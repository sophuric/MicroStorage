plugins {
    java
    alias(libs.plugins.fabric.loom)
    alias(libs.plugins.minotaur)
}

val modName = property("mod.name").toString()
val modId = property("mod.id").toString()
val modGroup = property("mod.group").toString()
val version = property("mod.version").toString()

base.archivesName = "${modId}-${version}"

loom {
    splitEnvironmentSourceSets()

    mods.create(modId) {
        sourceSet(sourceSets.getByName("main"))
        sourceSet(sourceSets.getByName("client"))
    }
}

repositories {
    mavenCentral()

    exclusiveContent {
        forRepository {
            maven("https://maven.terraformersmc.com/") {
                name = "Terraformers"
            }
        }

        filter {
            includeGroup("com.terraformersmc")
        }
    }
	
    exclusiveContent {
        forRepository {
            maven("https://raw.githubusercontent.com/SolidBlock-cn/mvn-repo/main") {
                name = "SolidBlock"
            }
        }

        filter {
            includeGroup("pers.solid")
        }
    }

    exclusiveContent {
        forRepository {
            maven("https://maven.parchmentmc.org") {
                name = "ParchmentMC"
            }
        }

        filter {
            includeGroup("org.parchmentmc.data")
        }
    }
}

dependencies {
    minecraft(libs.minecraft)

    @Suppress("UnstableApiUsage")
    mappings(
        loom.layered {
            officialMojangMappings()
            parchment(libs.parchment)
        }
    )

    modImplementation(libs.fabric.loader)
    modImplementation(libs.fabric.api)
    modImplementation(libs.brrp)
}

tasks.processResources {
    val map = mapOf(
        "mod_id" to modId,
        "mod_version" to version,
        "fabric_loader_version" to libs.versions.fabric.loader.get(),
        "fabric_api_version" to libs.versions.fabric.api.get(),
        "minecraft_version" to libs.versions.minecraft.get(),
        "brrp_version" to libs.versions.brrp.get().replaceAfterLast("-", ""), // remove Minecraft version from mod version
    )

    inputs.properties(map)
    filesMatching("fabric.mod.json") { expand(map) }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType<JavaCompile> {
    options.encoding = "UTF-8"
    options.release = 21
}

tasks.jar { from("LICENSE") { rename { "${it}_${base.archivesName.get()}" } } }

modrinth {
    token.set(System.getenv("MODRINTH_TOKEN"))
    projectId.set("microstorage")
    versionNumber.set(version)
    versionType.set("release")
    uploadFile.set(tasks.remapJar)
    loaders.add("fabric")
    dependencies {
        required.project("fabric-api")
        required.project("brrp")
    }
    syncBodyFrom = rootProject.file("README.md").readText()
}
